10 '4x4 character maker for RotG
20 *BOOT GOTO *
30 *SETCHAR
40 LOCATE 19,0
50 INPUT CHAR$
60 A=ASC(CHAR$)
70 INDEX=PEEK(MT+A)
80 IF INDEX<>0 THEN *GOTINDEXC
90 FOR I=1 TO 79
100  IF PEEK(CT+(I-1)*4)=0 THEN INDEX=I: POKE MT+A,INDEX: GOTO *GOTINDEXC
110 NEXT I
120 *GOTINDEXC CADDR=CT+(INDEX-1)*4
130 GOSUB *LOAD: GOSUB *DRAWUI
140 RETURN
150 *SETASC
160 LOCATE 31,0
170 INPUT A
180 IF A<0 OR A>255 THEN *SETASC
190 CHAR$=CHR$(A)
200 INDEX=PEEK(MT+A)
210 IF INDEX<>0 THEN *GOTINDEXA
220 FOR I=1 TO 79
230  IF PEEK(CT+(I-1)*4)=0 THEN INDEX=I: POKE MT+A,INDEX: GOTO *GOTINDEXA
240 NEXT I
250 *GOTINDEXA CADDR=CT+(INDEX-1)*4
260 GOSUB *LOAD: GOSUB *DRAWUI
270 RETURN
280 *SAVE
290 POKE &BFCA1,&40: LOCATE 10,2: PRINT "[S]ave": POKE &BFCA1,0
300 D=PX(&0)+PX(&1)*2+PX(&2)*4+PX(&3)*8: POKE CADDR,D
310 D=PX(&4)+PX(&5)*2+PX(&6)*4+PX(&7)*8: POKE CADDR+1,D
320 D=PX(&8)+PX(&9)*2+PX(&A)*4+PX(&B)*8: POKE CADDR+2,D
330 D=PX(&C)+PX(&D)*2+PX(&E)*4+PX(&F)*8: POKE CADDR+3,D
340 GOSUB *DRAWUI
350 RETURN
360 *LOAD
370 FOR I=0 TO 15: PX(I)=0: NEXT I
380 D=PEEK(CADDR): PX(&0)=D AND &1:PX(&1)=(D AND &2)/2:PX(&2)=(D AND &4)/4:PX(&3)=(D AND &8)/8
390 D=PEEK(CADDR+1): PX(&4)=D AND &1:PX(&5)=(D AND &2)/2:PX(&6)=(D AND &4)/4:PX(&7)=(D AND &8)/8
400 D=PEEK(CADDR+2): PX(&8)=D AND &1:PX(&9)=(D AND &2)/2:PX(&A)=(D AND &4)/4:PX(&B)=(D AND &8)/8
410 D=PEEK(CADDR+3): PX(&C)=D AND &1:PX(&D)=(D AND &2)/2:PX(&E)=(D AND &4)/4:PX(&F)=(D AND &8)/8
420 RETURN
430 *CLR
440 FOR I=0 TO 15: PX(I)=0: NEXT I
450 GOSUB *DRAWUI
460 RETURN
470 *RST
480 POKE &BFCA1,&40: LOCATE 25,2: PRINT "[R]eset": POKE &BFCA1,0
490 FOR I=0 TO 255: POKE MT+I,0: NEXT I
500 FOR I=0 TO 79: POKE CT+I*4,0,0,0,0: NEXT I
510 GOSUB *DRAWUI
520 RETURN
530 *DRAWUI
540 'PX ORDERS
550 '0  4  8  12
560 '1  5  9  13
570 '2  6  10 14
580 '3  7  11 15
590 LOCATE 0,0: IF PX(&0) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
600 LOCATE 0,1: IF PX(&1) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
610 LOCATE 0,2: IF PX(&2) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
620 LOCATE 0,3: IF PX(&3) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
630 LOCATE 2,0: IF PX(&4) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
640 LOCATE 2,1: IF PX(&5) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
650 LOCATE 2,2: IF PX(&6) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
660 LOCATE 2,3: IF PX(&7) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
670 LOCATE 4,0: IF PX(&8) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
680 LOCATE 4,1: IF PX(&9) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
690 LOCATE 4,2: IF PX(&A) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
700 LOCATE 4,3: IF PX(&B) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
710 LOCATE 6,0: IF PX(&C) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
720 LOCATE 6,1: IF PX(&D) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
730 LOCATE 6,2: IF PX(&E) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B1);CHR$(&B1) 
740 LOCATE 6,3: IF PX(&F) THEN PRINT CHR$(&DB);CHR$(&DB); ELSE PRINT CHR$(&B0);CHR$(&B0) 
750 LOCATE 10,0: PRINT "[C]har: '";CHAR$;"' [A]scii:";ASC(CHAR$)
760 LOCATE 10,1: PRINT "Address: &";HEX$(CADDR);" [";INDEX;"]"
770 LOCATE 10,2: PRINT "[S]ave c[L]ear [R]eset"
780 RETURN
790 *
800 CLS:CLEAR
810 POKE &BFCBF, PEEK(&BFCBF) AND NOT (2^7) 'disable key repeat
820 MT=&BBC00: CT=&BBD00
830 DIM PX(16): FOR I=0 TO 15: PX(I)=0: NEXT I
840 CADDR=CT: CHAR$=" "
850 GOSUB *DRAWUI
860 IF PEEK(CT)<>0 OR PEEK(CT+1)<>0 OR PEEK(CT+2)<>0 OR PEEK(CT+3)<>0 THEN *RST
870 *MAINLOOP
880 *GETINPUT
890 'KEY CODES
900 '55 56 57 47
910 '52 53 54 42
920 '49 50 51 45
930 '48 26 46 43
940 IF KC<>0 THEN OLDKC=KC
950 KC=ASC(INKEY$)
960 IF KC=0 THEN OLDKC=0
970 IF KC=OLDKC THEN GOTO *GETINPUT
980 IF KC=67 THEN GOSUB *SETCHAR
990 IF KC=83 THEN GOSUB *SAVE
1000 IF KC=76 OR KC=2 THEN GOSUB *CLR
1010 IF KC=65 THEN GOSUB *SETASC
1020 IF KC=82 THEN GOSUB *RST
1030 IF KC=55 THEN PX(&0)=1-PX(&0)
1040 IF KC=52 THEN PX(&1)=1-PX(&1)
1050 IF KC=49 THEN PX(&2)=1-PX(&2)
1060 IF KC=48 THEN PX(&3)=1-PX(&3)
1070 IF KC=56 THEN PX(&4)=1-PX(&4)
1080 IF KC=53 THEN PX(&5)=1-PX(&5)
1090 IF KC=50 THEN PX(&6)=1-PX(&6)
1100 IF KC=26 THEN PX(&7)=1-PX(&7)
1110 IF KC=57 THEN PX(&8)=1-PX(&8)
1120 IF KC=54 THEN PX(&9)=1-PX(&9)
1130 IF KC=51 THEN PX(&A)=1-PX(&A)
1140 IF KC=46 THEN PX(&B)=1-PX(&B)
1150 IF KC=47 THEN PX(&C)=1-PX(&C)
1160 IF KC=42 THEN PX(&D)=1-PX(&D)
1170 IF KC=45 THEN PX(&E)=1-PX(&E)
1180 IF KC=43 THEN PX(&F)=1-PX(&F)
1190 IF KC<>0 THEN GOSUB *DRAWUI
1200 GOTO *MAINLOOP
1210 *FIN
1220 END
